plugins {
    id 'java-library'
    id 'io.qameta.allure' version "2.12.0"
}

group = 'ru.example'
version = '1.0-SNAPSHOT'

allure {
    report {
        version.set("2.29.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.29.0")
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            platform("org.junit:junit-bom:5.13.4"),
            "org.junit.jupiter:junit-jupiter",
            "org.junit.platform:junit-platform-launcher",
            "com.codeborne:selenide:7.9.4",
            "io.qameta.allure:allure-selenide:2.29.1",
            "net.datafaker:datafaker:2.4.3"
    )
    testRuntimeOnly "org.slf4j:slf4j-nop:2.0.17"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "passed", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('test_regress', Test) {
    useJUnitPlatform {
        includeTags("regress")
    }
}

tasks.register('test_smoke', Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
}